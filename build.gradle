buildscript {
  ext {
    indraVersion = "1.0.2"
  }
}

plugins {
  id "net.kyori.indra" version "$indraVersion"
  id "net.kyori.indra.checkstyle" version "$indraVersion"
  id "net.kyori.indra.license-header" version "$indraVersion"
  id "net.kyori.indra.publishing" version "$indraVersion"
  id "net.kyori.indra.publishing.sonatype" version "$indraVersion"
}

group "net.kyori"
version "1.0.0-SNAPSHOT"
description "I prefer tea."

indra {
  github("KyoriPowered", "coffee")
  mitLicense()

  configurePublications {
    pom {
      developers {
        developer {
          id = "kashike"
          timezone = "America/Vancouver"
        }
      }
    }
  }
}

// TODO: indra?
tasks.withType(Sign) {
  onlyIf {
    (project.hasProperty("forceSign") || gradle.taskGraph.hasTask(":publish")) && !project.version.endsWith("-SNAPSHOT")
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://oss.sonatype.org/content/groups/public/"
  }
}

dependencies {
  api("org.checkerframework:checker-qual:3.7.0")
  checkstyle("ca.stellardrift:stylecheck:0.1")
  testImplementation("com.google.guava:guava-testlib:27.0.1-jre")
  testImplementation("com.google.truth:truth:0.42")
  testImplementation("com.google.truth.extensions:truth-java8-extension:0.42")
  testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
  testImplementation("org.junit.jupiter:junit-jupiter-engine:5.7.0")
}

jar {
  manifest.attributes(
    "Automatic-Module-Name": "net.kyori.coffee"
  )
}
